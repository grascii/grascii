
start : _preroot? _root _postroot? | _preroot
_preroot : prefix DISJOINER
_postroot : DISJOINER suffix

_root : ASPIRATE? _string "^"? ING~0..2
_string : _letter | _string ( _BOUNDARY | INTERSECTION )? _letter

prefix : _root 
suffix : _root

_letter : ASPIRATE? ( consonant | vowel )

consonant : standard_consonant
	| blended_consonant
	| annotatable_consonant	

standard_consonant : K | G | R | L | N | M
	| T | D | P | B | F | V
	| CH | J | X

blended_consonant.2 : _over_blend
	| _men
	| _gent
	| _def
	| _ses
	| NT | MT | ND | MD
	| DT | TD | DD
	| NG | NK
	| LD

_over_blend.2 : TN | DN | TM | DM
_men.3 : MN | MM
_gent.3 : JNT | JND | PND | PNT

_ses : ( SS | XS ) direction?

// this priority? ex. individual NDV
// what about -ntive? NTV
_def.-1 : DF | DV | TV

annotatable_consonant : directed_consonant | oblique_consonant

directed_consonant : ( S | Z | TH ) direction?

direction : LEFT | RIGHT

oblique_consonant : ( directed_consonant | SH ) OBLIQUE?

vowel : ( circle_vowel | hook_vowel | diphthong ) WUNDERBAR?

circle_vowel : ( A | E ) REVERSE? LOOP? sound?

sound : MEDIUM_SOUND | LONG_SOUND

hook_vowel : ( O LEFT? | U RIGHT? ) sound?

diphthong.2 : circle_diphthong | hook_diphthong

circle_diphthong : ( AE | EA ) REVERSE?
	| I REVERSE? LOOP?
 
hook_diphthong : EU | AU | OE

DISJOINER : "^"
_BOUNDARY : "-"
INTERSECTION : "\\"
ING : "'"

K : "K"
G : "G"
R : "R"
L : "L"
N : "N"
M : "M"
T : "T"
D : "D"
P : "P"
B : "B"
F : "F"
V : "V"
CH : "CH"
J : "J"
S : "S"
Z : "Z"
X : "X"
LD : "LD"
NG : "NG"
NK : "NK"
TN : "TN"
DN : "DN"
TM : "TM"
DM : "DM"
NT : "NT"
MT : "MT"
ND : "ND"
MD : "MD"
DF : "DF"
DV : "DV"
TV : "TV"
JNT : "JNT"
JND : "JND"
PND : "PND"
PNT : "PNT"
SS : "SS"
XS : "XS"
MN : "MN"
MM : "MM"
DT : "DT"
TD : "TD"
DD : "DD"
SH : "SH"
TH : "TH"
A : "A"
E : "E"
O : "O"
U : "U"
I : "I"
EA : "A&'"
// change to AE?
AE : "A&E"
EU : "EU"
AU : "AU"
OE : "OE"

ASPIRATE : "'"
LEFT : "("
RIGHT : ")"
OBLIQUE : ","
WUNDERBAR : "_"
MEDIUM_SOUND : "."
LONG_SOUND : ","
REVERSE : "~"
LOOP : "|"

%ignore " "











