start: _root ( DISJOINER _root ( DISJOINER _root? )? )?
  | _root DISJOINER
  | ASPIRATE

_root : ASPIRATE~0..2 _string ASPIRATE~0..2
_string : _letter | _string ASPIRATE? ( BOUNDARY | INTERSECTION )? _letter

_letter : ( consonant | vowel )

consonant : standard_consonant
	| blended_consonant
	| annotatable_consonant

standard_consonant : K | G | R | L | N | M
	| T | D | P | B | F | V
	| CH | J | X

blended_consonant.20 : _over_blend
	| _men
	| _gent
	| _def
	| _ses
	| NT | MT | ND | MD
	| DT | TD | DD
	| NG | NK
	| LD

_over_blend.20 : TN | DN | TM | DM
_men.30 : MN | MM
_gent.30 : JNT | JND | PND | PNT

_ses : ( SS | XS ) direction?

// this priority? ex. individual NDV
// what about -ntive? NTV
_def.-10 : DF | DV | TV

annotatable_consonant : ( directed_consonant | SH ) OBLIQUE?

directed_consonant : ( S | Z | TH ) direction?

direction : LEFT | RIGHT

vowel : ( circle_vowel | hook_vowel | diphthong ) WUNDERBAR?

circle_vowel : ( A | E ) REVERSE? LOOP? sound?

sound : MEDIUM_SOUND | LONG_SOUND

hook_vowel : ( O LEFT? | U RIGHT? ) sound?

diphthong.20 : circle_diphthong | hook_diphthong

circle_diphthong : ( AE | EA ) REVERSE?
	| I REVERSE? LOOP?

hook_diphthong : EU | AU | OE

DISJOINER : "^"
BOUNDARY : "-"
INTERSECTION : "\\"

// The terminal priorities are necessary to resolve conflicts with LALR.
// For Earley, the terminal priorities are insignificant.
// The rule priorities which carry ten times more weight will cause blended
// consonants to be preferred.

K : "K"
G : "G"
R : "R"
L : "L"
N.2 : "N"
M.2 : "M"
T : "T"
D.2 : "D"
P.2 : "P"
B : "B"
F : "F"
V : "V"
CH : "CH"
J.2 : "J"
S.2 : "S"
Z : "Z"
X.2 : "X"
LD : "LD"
NG : "NG"
NK : "NK"
TN : "TN"
DN : "DN"
TM : "TM"
DM : "DM"
NT : "NT"
MT : "MT"
ND : "ND"
MD : "MD"
DF : "DF"
DV : "DV"
TV : "TV"
JNT : "JNT"
JND : "JND"
PND : "PND"
PNT : "PNT"
SS : "SS"
XS : "XS"
MN : "MN"
MM : "MM"
DT : "DT"
TD : "TD"
DD : "DD"
SH.2 : "SH"
TH : "TH"
A : "A"
E : "E"
O : "O"
U : "U"
I : "I"
EA : "A&'"
// change to AE?
AE : "A&E"
EU : "EU"
AU : "AU"
OE : "OE"

ASPIRATE : "'"
LEFT : "("
RIGHT : ")"
OBLIQUE : ","
WUNDERBAR : "_"
MEDIUM_SOUND : "."
LONG_SOUND : ","
REVERSE : "~"
LOOP : "|"
