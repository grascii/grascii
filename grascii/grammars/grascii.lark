start: _root ( DISJOINER _root ( DISJOINER _root? )? )?
  | _root DISJOINER
  | ASPIRATE

_root : ASPIRATE~0..2 _string ASPIRATE~0..2
_string : _letter | _string ASPIRATE? ( BOUNDARY | INTERSECTION )? _letter

_letter : ( consonant | vowel )

consonant : standard_consonant
	| blended_consonant
	| annotatable_consonant

standard_consonant : K | G | R | L | N | M
	| T | D | P | B | F | V
	| CH | J

blended_consonant.20 : TN | DN | TM | DM
	| NT | MT | ND | MD
	| DF | DV | TV
	| JNT | JND | PND | PNT
	| MN | MM
	| DT | TD | DD
	| ( SS | XS ) direction?
	| NG | NK
	| LD

annotatable_consonant : ( directed_consonant | SH ) OBLIQUE? | X direction?

directed_consonant : ( S | Z | TH ) direction?

direction : LEFT | RIGHT

vowel : ( circle_vowel | hook_vowel | diphthong ) WUNDERBAR?

circle_vowel : ( A | E ) REVERSE? LOOP? sound?

sound : MEDIUM_SOUND | LONG_SOUND

hook_vowel : ( O LEFT? | U RIGHT? ) sound?

diphthong.20 : circle_diphthong | hook_diphthong

circle_diphthong : ( AE | EA ) REVERSE?
	| I REVERSE? LOOP?

hook_diphthong : EU | AU | OE

DISJOINER : "^"
BOUNDARY : "-"
INTERSECTION : "\\"

K : "K"
G : "G"
R : "R"
L : "L"
N : "N"
M : "M"
T : "T"
D : "D"
P : "P"
B : "B"
F : "F"
V : "V"
CH : "CH"
J : "J"
S : "S"
Z : "Z"
X : "X"
LD : "LD"
NG : "NG"
NK : "NK"
TN : "TN"
DN : "DN"
TM : "TM"
DM : "DM"
// Prevent matching before N/M so that over blends are preferred
NT : /NT(?![NM])/
MT : /MT(?![NM])/
ND : /ND(?![NM])/
MD : /MD(?![NM])/
DF : "DF"
DV : "DV"
TV : "TV"
JNT : "JNT"
JND : "JND"
PND : "PND"
PNT : "PNT"
SS : "SS"
XS : "XS"
MN : "MN"
MM : "MM"
DT : "DT"
TD : "TD"
DD : "DD"
SH : "SH"
TH : "TH"
A : "A"
E : "E"
O : "O"
U : "U"
I : "I"
EA : "A&'"
// change to AE?
AE : "A&E"
EU : "EU"
AU : "AU"
OE : "OE"

ASPIRATE : "'"
LEFT : "("
RIGHT : ")"
OBLIQUE : ","
WUNDERBAR : "_"
MEDIUM_SOUND : "."
LONG_SOUND : ","
REVERSE : "~"
LOOP : "|"
