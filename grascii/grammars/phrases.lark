
start : subject_phrase
	| prep_phrase
	| aux_phrase
	| adverb_phrase
	| as_phrase
	| time_phrase
	| infinitive
	| omitted_to
	| business_phrase
	| interrogative_phrase
	| command_phrase
	| conjunctive_phrase
	| described_noun

subject_phrase : conditional? ((pronoun (aux_phrase
	| (to_be NOT? (adjective* | adjective+ opt_to verb?))
	| (to_have | auxiliary | to_do) NOT?))
	| (pronoun verb to? verb? noun?))

prep_phrase : preposition ( adjective+ | noun | adverb )

noun.2 : described_noun | plain_noun

plain_noun : pronoun | common_noun
described_noun : adjective~1..2 common_noun

adjective : article | common_adjective | SUCH

aux_phrase : ( auxiliary ( NOT BE? | NOT? BE ) (ABLE? | adjective to? verb?) )
	| auxiliary? have_phrase
	| ( auxiliary NOT? to_have BEEN? ABLE? )
	| ( auxiliary NOT? verb )

have_phrase : to_have have_phrase_ending
have_phrase_ending : ( NOT BEEN? | NOT? BEEN ) ABLE?

adverb_phrase.2 : adverb ( to_be | auxiliary )

as_phrase : AS as_adj AS POSSIBLE?

// fix to guarantee 2
time_phrase : preposition? quantifier? unit AGO?

infinitive.2 : ( to | short_to ) ( verb | BE adjective? | HAVE have_phrase_ending )

!short_to : "T"

verb : common_verb | omitted_to_verb

omitted_to : ( omitted_to_verb opt_to verb )
	| ( preposition? omitted_to_noun  opt_to noun )

opt_to: to?

business_phrase : greeting | salutation | receipt_phrase

greeting : MY? DEAR title

// UT VTU UVT UR
salutation : ( YOURS VERY? sal_adverb )
	| ( VERY sal_adverb YOURS? )
	| ( VERY? sal_adverb YOURS )

// add NOT?
receipt_phrase : pronoun to_be opt_in RECEIPT opt_of opt_your noun?

opt_in: IN?
opt_of: OF?
opt_your: YOUR?

interrogative_phrase : interrogative ( ( noun verb? )
	| adjective+
	| adverb )

command_phrase : command ( DO? NOT? verb noun? | noun )

conjunctive_phrase : conjunction ( subject_phrase
	| adverb_phrase | to_be | preposition | adjective)

%import .vocab (DO, NOT, IN, RECEIPT, AS, MY, DEAR, YOURS, VERY, BE, BEEN, ABLE, SUCH, POSSIBLE, AGO, HAVE, YOUR, OF)

%import .vocab (common_verb, conjunction, unit, common_noun, pronoun, to_be, auxiliary, common_adjective, preposition, article, as_adj, omitted_to_verb, omitted_to_noun, title, sal_adverb, interrogative, adverb, conditional, quantifier, to_have, to_do, to, command)
